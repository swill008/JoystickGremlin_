// -*- coding: utf-8; -*-
// SPDX-License-Identifier: GPL-3.0-only

import QtQuick
import QtQuick.Controls

import Gremlin.Device
import Gremlin.Style

Button {
    id: _control

    property bool selected: false
    property Component deleteButton: null
    property Component editButton: null
    property string nameKey: ""
    property string defaultName: name
    property var liveState: null
    property int liveStamp: liveState && liveState.stamp !== undefined ? liveState.stamp : 0
    property string inputKind: liveState && index !== undefined ? liveState.kindAt(index) : ""
    property real liveValue: liveStamp >= 0 && liveState && index !== undefined ? liveState.valueAt(index) : 0

    readonly property bool _buttonActive: inputKind === "button" && liveValue > 0.5
    readonly property bool _hatActive: inputKind === "hat" && liveValue > 0.5
    readonly property bool _axisActive: inputKind === "axis"

    signal renameRequested()

    property int _descriptionWidth: 0
    property int _actionWidth: 0
    property bool _actionTruncated: false

    Connections {
        target: _control

        function onWidthChanged() {
            updateWidths()
        }
    }

    Connections {
        target: signal

        function onInputItemChanged(itemIndex) {
            if (itemIndex === index) {
                delayedUpdate.start()
            }
        }
    }

    Component.onCompleted: () => { updateWidths() }

    Timer {
        id: delayedUpdate
        interval: 50
        repeat: false
        onTriggered: updateWidths()
    }

    function updateWidths() {
        let imageWidth = _actionSequenceFull.item ? _actionSequenceFull.item.sourceSize.width : 0
        let widths = computeWidths(imageWidth, _inputDescription.text)

        _actionTruncated = widths[1] < imageWidth
        _descriptionWidth = widths[0]
        _actionWidth = widths[1]
    }

    function computeWidths(imageWidth, text) {
        let descriptionWidth = 0
        let actionWidth = 0

        let countWidth = 15
        let spacing = 30
        let textPadding = 10

        if (text.length == 0) {
            actionWidth = Math.min(_control.width, imageWidth)
        }
        else if (actionSequenceDisplayMode === "Count") {
            descriptionWidth = _control.width - countWidth - spacing
            actionWidth = countWidth
        }
        else {
            _textMetrics.text = text
            let textWidth = _textMetrics.width + textPadding

            let actionLimit = _control.width * 0.3
            let textLimit = _control.width * 0.7 - spacing

            if (imageWidth < actionLimit) {
                actionWidth = imageWidth
                descriptionWidth = Math.min(textWidth, _control.width - actionWidth - spacing)
            }
            else if (textWidth < textLimit) {
                descriptionWidth = textWidth + spacing
                actionWidth = Math.min(imageWidth, _control.width - descriptionWidth)
            }
            else {
                actionWidth = actionLimit
                descriptionWidth = textLimit
            }
        }

        return [descriptionWidth, actionWidth]
    }

    background: Rectangle {
        border.color: hovered ? Style.accent : selected ? Style.accent : Style.backgroundShade
        border.width: _buttonActive || _hatActive ? 2 : 1
        color: {
            if (_buttonActive || _hatActive) {
                return Qt.rgba(0.133, 0.773, 0.369, selected ? 0.55 : 0.38)
            }
            if (selected) {
                return Universal.chromeMediumColor
            }
            return Style.background
        }

        Rectangle {
            visible: _axisActive
            anchors.left: parent.left
            anchors.right: parent.right
            anchors.bottom: parent.bottom
            anchors.leftMargin: 1
            anchors.rightMargin: 1
            anchors.bottomMargin: 1
            height: 5
            color: Style.lowColor

            Rectangle {
                width: Math.max(0, Math.min(parent.width, parent.width * ((liveValue + 1.0) * 0.5)))
                height: parent.height
                color: "#22C55E"
            }
        }
    }

    contentItem: Item {
        JGText {
            id: _inputLabel
            text: name
            font.weight: 600

            width: Math.min(implicitWidth, parent.width - 48)
            elide: Text.ElideRight

            anchors.top: parent.top
            anchors.left: parent.left
        }

        Rectangle {
            visible: inputKind === "button" || inputKind === "hat"
            width: 10
            height: 10
            radius: 5
            anchors.top: parent.top
            anchors.topMargin: 4
            anchors.left: _inputLabel.right
            anchors.leftMargin: 8
            color: (_buttonActive || _hatActive) ? "#22C55E" : Style.lowColor
            border.width: 1
            border.color: (_buttonActive || _hatActive) ? "#16A34A" : Style.medColor
        }

        Loader {
            sourceComponent: _control.editButton

            anchors.top: parent.top
            anchors.left: _inputLabel.right
            anchors.leftMargin: (inputKind === "button" || inputKind === "hat") ? 22 : 0
        }

        Loader {
            active: actionSequenceDisplayMode === "Count"

            anchors.bottom: parent.bottom
            anchors.right: parent.right

            sourceComponent: Label {
                text: actionSequenceCount

                width: _actionWidth

                horizontalAlignment: Text.AlignRight
                verticalAlignment: Text.AlignVCenter
            }
        }

        Loader {
            id: _actionSequenceFull

            visible: actionSequenceDisplayMode === "Full"

            anchors.bottom: parent.bottom
            anchors.right: parent.right
            anchors.bottomMargin: _axisActive ? 6 : 0

            sourceComponent: Image {
                source: "image://action_summary/" + actionSequenceDescriptor
                    + "?r=" + (uiState ? uiState.themeRevision : 0)
                asynchronous: false
                cache: false
                clip: true

                width: _actionWidth
                height: sourceSize.height

                fillMode: Image.Pad
                horizontalAlignment: Image.AlignLeft
            }
        }

        JGText {
            id: _inputDescription
            text: description
            font.italic: true

            width: _descriptionWidth
            elide: Text.ElideRight

            anchors.left: parent.left
            anchors.bottom: parent.bottom
            anchors.bottomMargin: _axisActive ? 6 : 0
        }

        TextMetrics {
            id: _textMetrics
            font: _inputDescription.font
        }

        Loader {
            sourceComponent: _control.deleteButton

            anchors.top: parent.top
            anchors.right: parent.right
        }
    }

    HoverHandler {
        id: _hover
    }

    TapHandler {
        acceptedButtons: Qt.LeftButton
        onDoubleTapped: _control.renameRequested()
    }

    ToolTip {
        visible: _hover.hovered && actionSequenceDisplayMode === "Full" && _actionTruncated
        delay: 400

        x: _hover.point.position.x + 16
        y: _hover.point.position.y + 16

        contentItem: Image {
            source: _actionSequenceFull.item ? _actionSequenceFull.item.source : ""
            fillMode: Image.PreserveAspectFit
            width: implicitWidth
            height: implicitHeight
        }
    }

}
